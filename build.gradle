plugins {
    id "jacoco"
    id "idea"
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "org.jetbrains.kotlin.kapt" version "1.3.72"
    id "io.codearte.nexus-staging" version "0.11.0"
    id "maven"
    id "signing"
}

group = "io.openfuture"
version = '${VERSION}'
sourceCompatibility = JavaVersion.VERSION_12

repositories {
    mavenCentral()
}

dependencies {
    // Kotlin
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.50")

    // Tools
    compile("org.apache.httpcomponents:httpclient:4.5")

    // Jackson
    compile("com.fasterxml.jackson.core:jackson-databind:2.0.1")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1")

    // Test
    compile("junit:junit:4.12")
    compile("org.assertj:assertj-core:3.10.0")
    compile("org.mockito:mockito-all:1.10.19")
    compile("org.powermock:powermock-mockito-release-full:1.6.4")
}

compileKotlin {
    kotlinOptions.jvmTarget = "12"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "12"
}

// Jacoco
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'io/openfuture/sdk/*Application*',
                    'io/openfuture/sdk/domain/**',
                    'io/openfuture/sdk/exception/**',
            ])
        })
    }
}
check.dependsOn jacocoTestReport

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from "build/docs/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty("sign")) {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: '${OSSRHUSERNAME}', password: '${OSSRHPASSWORD}')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: '${OSSRHUSERNAME}', password: '${OSSRHPASSWORD}')
            }

            pom.project {
                name "open-api-jvm-sdk"
                packaging "jar"
                url "https://github.com/OpenFuturePlatform/open-api-jvm-sdk"
                description "OPEN Platform JVM SDK"

                licenses {
                    license {
                        name "MIT licence"
                        url "https://github.com/OpenFuturePlatform/open-api-jvm-sdk/blob/master/LICENCE.txt"
                    }
                }

                scm {
                    url "https://github.com/OpenFuturePlatform/open-api-jvm-sdk"
                }

                developers {
                    developer {
                        id "openplatform"
                        name "OPEN Platform"
                    }
                }
            }
        }
    }
}

nexusStaging {
    numberOfRetries = 40
    delayBetweenRetriesInMillis = 8000
}
